mutable struct CuSparseOperator{T,M,M2} <: AbstractSparseOperator{T}
  type::Type{T}
  m::Int
  n::Int
  A::M
  mat::M2
  transa::Char
  descA::CUSPARSE.CuSparseMatrixDescriptor
  buffer::CuVector{UInt8}
  spmm_buffer::CuVector{UInt8}
  alpha::Base.RefValue{T}
  beta::Base.RefValue{T}
end

Base.eltype(A::CuSparseOperator{T}) where {T} = T
Base.size(A::CuSparseOperator) = (A.m, A.n)
SparseArrays.nnz(A::CuSparseOperator) = nnz(A.A)
operator_sparse_matrix(A::CuSparseOperator) = A.A

function _coo_to_cu_csr(A::SparseMatrixCOO{Tv, Ti}) where {Tv, Ti}
  return CUSPARSE.CuSparseMatrixCSR(sparse(A.rows, A.cols, A.vals, size(A)...))
end

for SparseMatrixType in (:(CuSparseMatrixCSR{T}), :(CuSparseMatrixCSC{T}), :(CuSparseMatrixCOO{T}))
  @eval begin
    function gpu_operator(A::$SparseMatrixType; transa::Char = 'N', symmetric::Bool = false, spmm_ncols::Int = 0) where {T <: BlasFloat}
      m, n = size(A)
      alpha = Ref{T}(one(T))
      beta = Ref{T}(zero(T))
      bool = symmetric && (nnz(A) > 0)
      mat = bool ? tril(A, -1) + A' : A
      descA = CUSPARSE.CuSparseMatrixDescriptor(mat, 'O')
      descX = CUSPARSE.CuDenseVectorDescriptor(T, n)
      descY = CUSPARSE.CuDenseVectorDescriptor(T, m)
      algo = CUSPARSE.CUSPARSE_SPMV_ALG_DEFAULT
      buffer_size = Ref{Csize_t}()
      CUSPARSE.cusparseSpMV_bufferSize(CUSPARSE.handle(), transa, alpha, descA, descX, beta, descY, T, algo, buffer_size)
      buffer = CuVector{UInt8}(undef, buffer_size[])
      if CUSPARSE.version() ≥ v"12.3"
        CUSPARSE.cusparseSpMV_preprocess(CUSPARSE.handle(), transa, alpha, descA, descX, beta, descY, T, algo, buffer)
      end
      M = typeof(A)
      M2 = typeof(mat)
      spmm_buffer = if spmm_ncols > 0
        descB = CUSPARSE.CuDenseMatrixDescriptor(T, n, spmm_ncols)
        descC = CUSPARSE.CuDenseMatrixDescriptor(T, m, spmm_ncols)
        spmm_buf_size = Ref{Csize_t}()
        spmm_algo = CUSPARSE.CUSPARSE_SPMM_ALG_DEFAULT
        CUSPARSE.cusparseSpMM_bufferSize(CUSPARSE.handle(), transa, 'N', alpha, descA, descB, beta, descC, T, spmm_algo, spmm_buf_size)
        buf = CuVector{UInt8}(undef, spmm_buf_size[])
        if CUSPARSE.version() ≥ v"12.3"
          CUSPARSE.cusparseSpMM_preprocess(CUSPARSE.handle(), transa, 'N', alpha, descA, descB, beta, descC, T, spmm_algo, buf)
        end
        buf
      else
        CuVector{UInt8}(undef, 0)
      end
      return CuSparseOperator{T, M, M2}(T, m, n, A, mat, transa, descA, buffer, spmm_buffer, alpha, beta)
    end
  end
end

function LinearAlgebra.mul!(Y::CuMatrix{T}, A::CuSparseOperator{T}, X::CuMatrix{T}) where {T <: BlasFloat}
  (size(Y, 1) != A.m) && throw(DimensionMismatch("size(Y,1) != A.m"))
  (size(X, 1) != A.n) && throw(DimensionMismatch("size(X,1) != A.n"))
  descX = CUSPARSE.CuDenseMatrixDescriptor(X)
  descY = CUSPARSE.CuDenseMatrixDescriptor(Y)
  CUSPARSE.cusparseSpMM(
    CUSPARSE.handle(), A.transa, 'N',
    A.alpha, A.descA, descX, A.beta, descY,
    T, CUSPARSE.CUSPARSE_SPMM_ALG_DEFAULT, A.spmm_buffer,
  )
end

function LinearAlgebra.mul!(y::CuVector{T}, A::CuSparseOperator{T}, x::CuVector{T}) where {T <: BlasFloat}
  (length(y) != A.m) && throw(DimensionMismatch("length(y) != A.m"))
  (length(x) != A.n) && throw(DimensionMismatch("length(x) != A.n"))
  descY = CUSPARSE.CuDenseVectorDescriptor(y)
  descX = CUSPARSE.CuDenseVectorDescriptor(x)
  algo = CUSPARSE.CUSPARSE_SPMV_ALG_DEFAULT
  CUSPARSE.cusparseSpMV(CUSPARSE.handle(), A.transa, A.alpha, A.descA, descX, A.beta, descY, T, algo, A.buffer)
end

function LinearAlgebra.mul!(Y::CuMatrix{T}, A::CuSparseOperator{T}, X::CuMatrix{T}, α::Number, β::Number) where {T <: BlasFloat}
  A.alpha[] = T(α)
  A.beta[] = T(β)
  mul!(Y, A, X)
  A.alpha[] = one(T)
  A.beta[] = zero(T)
  return Y
end
